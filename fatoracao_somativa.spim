# O OBJETIVO DESSE CODIGO EH RECEBER UM NUMERO NATURAL E IMPRIMIR QUAIS NUMEROS 
# QUE AO SEREM MULTIPLICADOS RESULTAM NESSE DADO NUMERO, OU SEJA, FATORACAO.
.data
    quebraLinha: .asciiz "\n"         
    incorreta:   .asciiz "Entrada invalida.\n"  
.text
    main:
        li      $v0, 5           # lendo o numero 
        syscall

        move    $t0, $v0         # movendo numero lido para t0
        li      $t1, 32768
        li      $t2, 1
        slt     $t3, $t0, $t1         # se o numero for maior ou igual a 32768
        beq     $t3, $zero, invalido
        slt     $t3, $t2, $t0         # se o numero for menor que 2
        beq     $t3, $zero, invalido 

        li      $t4, 2           # aux = 2
    LOOP:
        beq     $t0, $t2, Exit       # se numero for igual a 1, sai do loop
        div     $t0, $t4             # dividindo o numero por aux
        mfhi    $t5                  # movendo o resto, que esta no HI, para t5
        bne     $t5, $zero, Else     # se o resto for diferente de zero, vai pro else

        move    $a0, $t4             # imprimindo aux
        li      $v0, 1
        syscall

        li      $v0, 4
        la      $a0, quebraLinha
        syscall

        mflo    $t0                 # numero = numero / aux
        j       LOOP
    Else:
        addi    $t4, $t4, 1         # aux++
        j		LOOP				
    invalido:
        li      $v0, 4
        la      $a0, incorreta
        syscall
    Exit:
        li      $v0, 10
        syscall
